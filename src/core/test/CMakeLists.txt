# linux needs to explicitly ask for pthreads
if (UNIX AND NOT APPLE)
  set(THREADS_PREFER_PTHREAD_FLAG ON)
  find_package(Threads REQUIRED)
endif()

# adding support for checking the tests with valgrind
if(RUN_VALGRIND_TESTS)
     find_package(Valgrind REQUIRED)
     if(VALGRIND_FOUND)
         set(CTEST_MEMORYCHECK_COMMAND ${VALGRIND_PROGRAM})
         set(MEMORYCHECK_COMMAND ${VALGRIND_PROGRAM})
         if (APPLE)
             set(MEMORYCHECK_SUPPRESSIONS "--suppressions=${PROJECT_SOURCE_DIR}/cmake/valgrind-macos.supp")
         else ()
             set(MEMORYCHECK_SUPPRESSIONS "")
         endif ()
         set(MEMORYCHECK_COMMAND_OPTIONS "--leak-check=full --error-exitcode=1 ${MEMORYCHECK_SUPPRESSIONS}"  CACHE STRING "Options to pass to the memory checker")
         mark_as_advanced(MEMORYCHECK_COMMAND_OPTIONS)
         set(MEMCHECK_COMMAND_COMPLETE "${MEMORYCHECK_COMMAND} ${MEMORYCHECK_COMMAND_OPTIONS}")
         separate_arguments(MEMCHECK_COMMAND_COMPLETE)
     endif()
endif()

macro(add_sup_test classname)
    set(testsrc ${classname}Test.cpp)
    set(testbinary ${classname}UnitTest)
    set(testname   UnitTest${classname})
    add_executable(${testbinary} ${testsrc})
    if (UNIX AND NOT APPLE)
        target_link_libraries(${testbinary} PRIVATE step_up_planner::core Threads::Threads)
    else()
        target_link_libraries(${testbinary} PRIVATE step_up_planner::core)
    endif()

    add_test(NAME ${testname} COMMAND ${testbinary})

    if(RUN_VALGRIND_TESTS)
        add_test(NAME memcheck_${testname} COMMAND ${MEMCHECK_COMMAND_COMPLETE} $<TARGET_FILE:${testbinary}>)
    endif()
endmacro()

add_sup_test(FiveAndThreePhases)
add_sup_test(Rotation)

